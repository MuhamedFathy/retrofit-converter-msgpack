/**
 * The MIT License (MIT)
 *
 * Copyright (c) 2015 Dmytro Dmytrenko, <dmytrenko.d@gmail.com>
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and
 * associated documentation files (the "Software"), to deal in the Software without restriction,
 * including without limitation the rights to use, copy, modify, merge, publish, distribute,
 * sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or
 * substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT
 * NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT.  IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
 * DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

import java.text.SimpleDateFormat

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {

    testCompile dependencyAssertJCore
    testCompile dependencyJUnit
    testCompile dependencyOkHttpMockWebServer

    compile dependencyMsgPack
    compile dependencyRetrofit
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {

    publications {

        converter(MavenPublication) {
            artifactId ARTIFACT

            from components.java

            artifact javadocJar
            artifact sourcesJar

            pom.withXml {
                asNode().children().last() + {

                    resolveStrategy = Closure.DELEGATE_FIRST

                    name POM_NAME
                    description POM_DESCRIPTION

                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENSE_NAME
                            url POM_LICENSE_URL
                            distribution POM_LICENSE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                            email POM_DEVELOPER_EMAIL
                        }
                    }
                }
            }
        }
    }
}

bintray {

    try {
        user = property('bintrayUsername')
        key = property('bintrayApiKey')
    } catch (Throwable ignored) {
        user = ""
        key = ""
    }

    publications = ['converter']

    dryRun = false
    publish = true

    pkg {

        repo = BINTRAY_REPO
        name = BINTRAY_NAME
        desc = BINTRAY_DESC

        websiteUrl = BINTRAY_WEBSITE_URL
        issueTrackerUrl = BINTRAY_ISSUE_TRACKER_URL
        vcsUrl = BINTRAY_VCS_URL

        licenses = [BINTRAY_LICENSE]
        labels = [
                BINTRAY_LABEL_ANDROID,
                BINTRAY_LABEL_CONVERTER,
                BINTRAY_LABEL_JAVA,
                BINTRAY_LABEL_MSGPACK,
                BINTRAY_LABEL_RETROFIT
        ]

        publicDownloadNumbers = true

        version {

            name = VERSION_NAME
            desc = BINTRAY_DESC

            // Considering this issue:
            // https://github.com/ametiste-oss/ametiste-bintray-gradle/issues/1
            // We will use ISO date format
            def timeZone = TimeZone.getTimeZone("UTC");
            def dateFormat = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZZ");
            dateFormat.setTimeZone(timeZone);
            released = dateFormat.format(new Date());

            gpg {
                sign = true

                try {
                    passphrase = property('gpgPassphrase')
                } catch (Throwable ignored) {
                    passphrase = ""
                }
            }
        }
    }
}